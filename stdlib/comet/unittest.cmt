class Assert {
    init(value) {
        self.value = value
    }

    is_nil(message=nil) {
        assert(self.value.nil?(), message)
    }

    is_not_nil(message=nil) {
        assert(self.value.nil?() == false, message)
    }

    is_equal_to(expected, message=nil) {
        assert(self.value == expected, message)
    }

    is_true(message=nil) {
        assert(self.value, message)
    }

    is_false(message=nil) {
        assert(!self.value, message)
    }

    contains(expected, message=nil) {
        assert(self.value.contains?(expected), message)
    }

    matches(value, message=nil) {
        if (callable?(value)) {
            assert(value(self.value) == true, message)
        } else {
            is_equal_to(value, message)
        }
    }

    throws(message=nil) {
        if (!callable?(self.value)) {
            throw ArgumentException(String.format('throws() expects a callable to assert, but got {0}', self.value))
        }
        try {
            self.value()
            assert(false, message)
        } catch (Exception as ex) {
            if (ex is AssertionException) {
                rethrow ex
            }
            assert(true, message)
        }
    }

    does_not_throw(message=nil) {
        if (!callable?(self.value)) {
            throw ArgumentException(String.format('does_not_throw() expects a callable to assert, but got {0}', self.value))
        }
        try {
            self.value()
            assert(true, message)
        } catch (Exception as ex) {
            if (ex is AssertionException) {
                rethrow ex
            }
            assert(false, message)
        }
    }

    is_of_type(type, message=nil) {
        assert(self.value is type, message)
    }

    is_empty(message=nil) {
        assert(self.value.empty?(), message)
    }

    static that(value) {
        return Assert(value)
    }

    static fail(message) {
        assert(false, message)
    }

    static pass() {}
}

if (__MAIN__ == __FILE__) {
    var tests = {}
    var total = 0
    var passed = 0
    foreach (var to_import in ARGV) {
        import to_import as imported
        foreach (var func in imported.functions()) {
            if (func.starts_with?('test_')) {
                total += 1
                var test_name = String.format('{0}:{1}', imported.filename(), func)
                tests[test_name] = true
                try {
                    call_function(imported, func)
                    print(String.format('✓ {0} passed', test_name))
                    passed += 1
                } catch (Exception as ex) {
                    tests[test_name] = false
                    print(String.format('⨯ {0} failed', test_name))
                    print(ex.class_name(), ':')
                    print(ex.stacktrace)
                }
            }
        }
    }
    print(String.format('{0}/{1} tests passed', passed, total))
    if (passed != total) {
        exit(128)
    }
}
